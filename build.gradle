buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.1.2.RELEASE'
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
}

apply plugin: 'spring-cloud-contract'
apply plugin: 'io.spring.dependency-management'

group = 'ru.alfabank.contracts'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

idea {
	module {
		outputDir file('build/classes/main')
		testOutputDir file('build/classes/test')
	}
}

contracts {
	baseClassForTests = 'ru.alfabank.contracts.jessepinkman.BaseContractsTest'
	contractsPath = '/'
	contractsDslDir = new File("${project.rootDir}/src/test/resources/contracts")
}

publishStubsToScm {
	customize {
		contractDependency {
			stringNotation = "${project.group}:${project.name}:${project.version}"
		}
		contractRepository {
			repositoryUrl = "git://git@github.com:artemptushkin/spring-cloud-contract-git-repo.git"
		}
		contractsMode = 'REMOTE'
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Greenwich.SR2")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
